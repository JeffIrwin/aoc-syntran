
#include("../../utils.syntran");
#include("../../dict_str.syntran");
#include("../../random.syntran");

//==============================================================================

let ifile = 0;
ifile = 1;

// Part 2 uses a different test input
let filenames =
[
	"test-input.txt",
	"input.txt"
];
let filename = filenames[ifile];

//==============================================================================

fn shuffle_str_2(a: &[str; :,:], seed: i32)
{
	// Shuffle a rank-2 str array using the Fisher-Yates shuffle algorithm
	//
	// TODO: utils or other common file?  This depends on random.syntran
	seed_mt19937(seed);
	for i in [i32(size(a, 1))-1: -1: 0]
	{
		let j = pmod(rand_i32_mt19937(), i+1);
		//println("i, j = ", [i, j]);
		a[:, [i,j]] = a[:, [j,i]];
	}
	return;
}

fn is_lower(char_: str): bool
{
	return i32("a") <= i32(char_) and i32(char_) <= i32("z");
}

fn part1(): str
{
	let sum_ = 0'i64;

	let f = open(filename, "r");

	// Load replacements
	let reps = new_dict_str();
	let str_ = readln(f);
	//while not eof(f)
	while str_ != ""
	{
		//println("str_ = ", str_);

		let words = split_(str_, " => ");
		let atom = words[0];
		let rep = words[1];
		//println("atom, rep = ", [atom, rep]);

		let rep0 = get_dict_str(&reps, atom);

		// Delimit by colons.  We'll split later
		set_dict_str(&reps, atom, rep0 + rep + ":");

		str_ = readln(f);
	}

	// Initial molecule
	let mole = readln(f);
	//println("mole = ", mole);

	//// Debug only
	//for key in keys_dict_str(&reps)
	//{
	//	let val = get_dict_str(&reps, key);
	//	println("key, val = ", [key, val]);
	//}

	let new_moles = new_dict_str();

	// Process molecule
	let i = 0;
	//for i in [0: len(mole)]
	while i < len(mole)
	{
		let i0 = i;
		//let new_mole = mole[0: i];

		let atom = mole[i];
		i += 1;
		while is_lower(mole[i])
		{
			atom += mole[i];
			i += 1;
		}
		//println("atom = ", atom);

		//// Split had a bug for empty strings, which is now fixed, that this
		//// worked around
		//let atom_reps = get_dict_str(&reps, atom);
		//if (atom_reps == "") continue;
		//let reps = split_(atom_reps, ":");  // shadowing

		let reps = split_(get_dict_str(&reps, atom), ":"); // shadowing
		//println("reps = ", reps);

		for rep in reps
		{
			let new_mole = mole[0: i0] + rep + mole[i: len(mole)];
			//println("new_mole = ", new_mole);
			set_dict_str(&new_moles, new_mole, "exists");
		}
		//println();
	}
	sum_ = len_dict_str(&new_moles);

	println("part 1 = ", sum_);
	return str(sum_);
}

fn get_match_len(a: str, b: str): i32
{
	// Count the length of the shared matching prefix of strings `a` and `b`
	let n = min(len(a), len(b));
	let i = 0;
	while i < n
	{
		if (a[i] != b[i]) break;
		i += 1;
	}
	return i;
}

fn part2(): str
{
	let sum_ = 0'i64;

	let f = open(filename, "r");

	// In part 2, invert the replacement transformations and work backwards from
	// the target molecule towards "e".  Each replacement RHS is unique, so this
	// can be done with a simple array instead of a dict.  Also, an array is
	// easier to shuffle than a dict
	//
	// I had to cheat by looking at reddit.  Reference:
	//
	//     https://www.reddit.com/r/adventofcode/comments/3xflz8/comment/cy4kpms/?utm_source=share&utm_medium=web3x&utm_name=web3xcss&utm_term=1&utm_content=share_button
	//

	let NREP_CAP = 128;
	let nrep = 0;

	// Load replacements
	let reps = [""; 2, NREP_CAP];
	let str_ = readln(f);
	while str_ != ""
	{
		//println("str_ = ", str_);

		let words = split_(str_, " => ");
		let atom = words[0];
		let rep = words[1];
		//println("atom, rep = ", [atom, rep]);

		reps[:, nrep] = [atom, rep];
		nrep += 1;

		str_ = readln(f);
	}
	reps = reps[:, 0: nrep];

	// Change the seed of this shuffle to test the robustness of the algorithm
	shuffle_str_2(&reps, 2);
	//println("reps = ", reps);

	// Target medicine molecule
	let medi = readln(f);
	//println("medi = ", medi);

	let count_ = 0;
	let shuffles = 0;
	let mol = medi;
	while len(mol) > 1
	{
		let start = mol;
		for i in [0: nrep]
		{
			let idx = index_(mol, reps[1,i]);
			while idx >= 0
			{
				count_ += 1;
				//println("replacing ", reps[:,i]);
				//println("idx = ", idx);
				//println("before = ", mol);
				mol = mol[0: idx] + reps[0,i] + mol[idx + len(reps[1,i]): len(mol)];
				//println("after  = ", mol);
				//println("count_ = ", count_);

				idx = index_(mol, reps[1,i]);
			}
		}
		if start == mol
		{
			mol = medi;
			count_ = 0;
			//println("no more reps");
			//println("shuffles = ", shuffles);

			shuffle_str_2(&reps, shuffles);
			shuffles += 1;
		}
	}
	sum_ = count_;

	//println("shuffles = ", shuffles);
	println("part 2 = ", sum_);

	return str(sum_);
}

fn part2old(): str
{
	let sum_ = 0'i64;

	let f = open(filename, "r");

	// Load replacements
	let reps = new_dict_str();
	let str_ = readln(f);
	while str_ != ""
	{
		println("str_ = ", str_);

		let words = split_(str_, " => ");
		let atom = words[0];
		let rep = words[1];
		println("atom, rep = ", [atom, rep]);

		let rep0 = get_dict_str(&reps, atom);

		// Delimit by colons.  We'll split later
		set_dict_str(&reps, atom, rep0 + rep + ":");

		str_ = readln(f);
	}

	// Target medicine molecule
	let medi = readln(f);
	println("medi = ", medi);

	// Debug only
	for key in keys_dict_str(&reps)
	{
		let val = get_dict_str(&reps, key);
		println("key, val = ", [key, val]);
	}

	let new_moles = new_dict_str();

	// Do a BFS (or DFS?).  Index the target medicine for each newly generated
	// molecule.  If it doesn't exist as a substring, don't bother pushing it to
	// the queue.  Actually, that last part isn't right, because a
	// transformation can change an atom in the middle of the string.  Might
	// have to brute the whole BFS

	let CAP = 1024 * 8192;

	// Queue of molecules
	let q = [""; CAP];
	let qs = 0;  let qe = 0;  // start and end queue pointers/indices
	
	// Queue of distances or steps from start of process.  Shares same qs/qe
	let qd = [0; CAP];
	
	// Dict of molecules that we've already seen before, to avoid redundant
	// reprocessing and infinite loops
	let seen = new_dict_str();

	// Start with just "e" as the molecule
	let mole = "e";

	// Push to queues but not dict yet
	q [qe] = mole;
	qd[qe] = 0;  // step distance 0
	qe += 1;
	//set_dict_str(&seen, mole, "exists");

	let match_len_max = 0;

	while true
	{
		if (qs >= qe)
		{
			println("Error: queue is empty");
			exit(-1);
		}

		// Pop
		mole = q [qs];
		let dist = qd[qs];
		qs += 1;
		//println("mole = ", mole);

		// Have we visited this graph node before?
		if (get_dict_str(&seen, mole) != "") continue;
		set_dict_str(&seen, mole, "exists");

		println("mole = ", mole);
		println("match_len_max = ", match_len_max);
		//println("dist = ", dist);

		// Check if we found the target medicine
		if mole == medi
		{
			sum_ = dist;
			break;
		}

		let match_len0 = get_match_len(mole, medi);
		match_len_max = max(match_len_max, match_len0);

		//if (match_len0 < match_len_max - 10) continue;
		//if (match_len_max > 20 and match_len0 < match_len_max - 7) continue; // -6 gets stuck at 78

		// Process replacement transformations for this molecule
		//
		// Since I'm pruning poor prefix matches, it might be better to iterate
		// backwards over `i`, but I would have to change the is_lower() logic

		let i = 0;
		//let i = match_len0;
		//let i = max(0, match_len0 - 2);
		//while is_lower(mole[i])
		//	i += 1;

		while i < len(mole)
		{
			let i0 = i;
			let atom = mole[i];
			i += 1;
			while is_lower(mole[i])
			{
				atom += mole[i];
				i += 1;
			}
			//println("atom = ", atom);

			let reps = split_(get_dict_str(&reps, atom), ":"); // shadowing
			//println("reps = ", reps);

			for rep in reps
			{
				let new_mole = mole[0: i0] + rep + mole[i: len(mole)];
				//println("new_mole = ", new_mole);

				let match_len = get_match_len(new_mole, medi);
				match_len_max = max(match_len_max, match_len);

				//if (match_len < match_len0 - 1) continue;
				if (match_len0 > 20 and match_len < match_len0 - 0) continue;

				if (match_len0 > 20 and match_len < match_len_max - 2) continue;
				if (match_len < match_len_max - 4) continue;
				//if (match_len < match_len_max) continue;

				if (len(new_mole) > len(medi) + 6) continue;

				// Push queues
				q [qe] = new_mole;
				qd[qe] = dist + 1;
				qe += 1;
				if qe >= CAP
				{
					println("Error: queue overflow!");
					exit(-1);
				}
			}
		}
	}

	println("part 2 = ", sum_);
	return str(sum_);
}

//==============================================================================

fn main(): str
{
	// For unit-testing (within the syntran repo), this main fn returns a value
	// to be compared with expected results
	println();
	println("Starting AOC syntran main 2015/19");

	let p1 = ""; let p2 = "";

	p1 = part1();
	p2 = part2();

	println("Ending AOC syntran main");
	println();
	return p1 + ":" + p2;
}

//==============================================================================

return main();

//==============================================================================

