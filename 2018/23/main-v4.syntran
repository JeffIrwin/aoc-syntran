
#include("../../utils.syntran");
#include("../../dict_i64.syntran");

//==============================================================================

let ifile = 0;
ifile = 1;

let filenames =
[
	"test-input.txt",
	"input.txt"
];
let filename = filenames[ifile];

//==============================================================================

fn part1(): str
{
	let n = countln_(filename);
	let pos = [0; 3, n];
	let range = [0; n];

	let f = open(filename, "r");
	for i in [0: n]
	{
		let nums = read_mi32s(readln(f));
		pos[:,i] = nums[0: 3];
		range[i] = nums[3];
	}
	close(f);

	let rmax = -1;
	let imax = -1;
	for i in [0: n]
		if range[i] > rmax
		{
			rmax = range[i];
			imax = i;
		}
	println("imax, rmax = ", [imax, rmax]);

	let pmax = pos[:, imax];
	let sum_ = 0'i64;
	for i in [0: n]
	{
		if sum(abs(pos[:,i] - pmax)) <= rmax
			sum_ += 1;
	}

	println("part 1 = ", sum_);
	return str(sum_);
}

// 6 cardinal directions in 3d
let NDIRS = 6;
let DIRS =
[
	-1,  0,  0,
	+1,  0,  0,
	 0, -1,  0,
	 0, +1,  0,
	 0,  0, -1,
	 0,  0, +1
	;
	3, NDIRS
];

//let nu = count_particles(&u, &pos, &range);
fn count_particles(u: &[i32; :], pos: &[i32; :,:], range: &[i32; :]): i32
{
	// Count number of particles in range `nu`
	let nu = 0;
	for i in [0: size(range, 0)]
		if sum(abs(u - pos[:,i])) <= range[i]
			nu += 1;
	return nu;
}

fn part2(): str
{
	let n = countln_(filename);
	let pos = [0; 3, n];
	let range = [0; n];

	let f = open(filename, "r");
	for i in [0: n]
	{
		let nums = read_mi32s(readln(f));
		pos[:,i] = nums[0: 3];
		range[i] = nums[3];
	}
	close(f);

	let xmin = minval(pos[0,:]);
	let ymin = minval(pos[1,:]);
	let zmin = minval(pos[2,:]);
	let xmax = maxval(pos[0,:]) + 1;
	let ymax = maxval(pos[1,:]) + 1;
	let zmax = maxval(pos[2,:]) + 1;
	println("x in ", [xmin, xmax]);
	println("y in ", [ymin, ymax]);
	println("z in ", [zmin, zmax]);

	let nx = xmax - xmin;
	let ny = ymax - ymin;
	let nz = zmax - zmin;

	//let sum_ = 0'i64;

	//
	// TODO: solve this like an optimization problem.  Start with some point,
	// maybe the middle of the bounding box or the weighted centroid.  Count the
	// number of particles in range, then do the same for its neighbors.
	// Advance to any neighbor that is in range of more.  Might have to search
	// more than 1 step as immediate neighbors could be in range of the same
	// number
	//
	// Actually, start point should probably be the part 1 solution to avoid
	// getting stuck in local minima.  Or maybe need to re-try optimization with
	// each particle as the initial guess
	//

	println("DIRS = ", DIRS);

	let visited = new_dict_i64();

	let QCAP = 1024 * 256;
	let q = [-1; 3, QCAP];
	let qs = 0;  let qe = 0;

	// Push
	q[:, qe] = pos[:,0];  qe += 1;
	//q[:, qe] = [xmin, ymin, zmin];  qe += 1;
	//q[:, qe] = ([xmin, ymin, zmin] + [xmax, ymax, zmax]) / 2;  qe += 1;

	// Uphill BFS towards points in range of more particles
	let nmax = -1;
	let sum_ = 2_000_000_000;
	while qs != qe
	{
		// Pop
		let u = q[:, qs];  qs = (qs+1) % QCAP;

		if (not in_bounds([xmin, ymin, zmin], u, [xmax, ymax, zmax])) continue;

		let key = str(u);
		let seen = get_dict_i64(&visited, key);
		if (seen == 1) continue;
		set_dict_i64(&visited, key, 1'i64);

		let nu = count_particles(&u, &pos, &range);

		if (nu < nmax) continue;
		let s = sum(abs(u));

		if (nu > nmax)
		{
			//println("s = ", s);
			println("nu = ", nu);
			sum_ = s;
		}
		nmax = nu;
		sum_ = min(sum_, s);

		//println("u, nu = ", [u, [nu]]);

		for d in [0: NDIRS]
		{
			//let step = 0;
			let step = 1;

			let nv = 0;
			let v = u;
			let v0 = v;
			while true
			{
				//println("        step = ", step);
				//step += 1;
				v = u + step * DIRS[:,d];
				if (not in_bounds([xmin, ymin, zmin], v, [xmax, ymax, zmax])) break;
				v0 = v;
				step *= 2;
				nv = count_particles(&v, &pos, &range);
				if (step < 0) break;
				//println("         v = ",  v);
				//println("        nv = ", nv);
				if (nv != nu) break;
			}
			//println("    nv = ", nv);
			if (nv < nu) continue;

			//if (nv > nu) println("nv = ", nv);
			if (nv > nu) println("v, nv = ", [v, [nv]]);

			//v = v0;

			// Push
			q[:, qe] = v;  qe = (qe+1) % QCAP;
			if qe == qs
			{
				println("Error:  queue overflow!");
				exit(-3);
			}
		}
	}

	println("part 2 = ", sum_);
	return str(sum_);
}

//==============================================================================

fn main(): str
{
	// For unit-testing (within the syntran repo), this main fn returns a value
	// to be compared with expected results
	println();
	println("Starting AOC syntran main 2018/23");

	let p1 = ""; let p2 = "";

	//p1 = part1(); // TODO
	p2 = part2();

	println("Ending AOC syntran main");
	println();
	return p1 + ":" + p2;
}

//==============================================================================

return main();

//==============================================================================

