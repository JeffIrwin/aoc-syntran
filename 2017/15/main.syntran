
#include("../../utils.syntran");
//#include("../../dict_i64.syntran");

//==============================================================================

let ifile = 0;
ifile = 1;

let filenames =
[
	"test-input.txt",
	"input.txt"
];
let filename = filenames[ifile];

//==============================================================================

fn part1(): str
{
	let sum_ = 0'i64;

	let f = open(filename, "r");
	let a = i64(read_i32(readln(f)));
	let b = i64(read_i32(readln(f)));

	//println("a = ", a);
	//println("b = ", b);

	for i in [0: 40_000_000]
	{
		a = (a * 16_807) % 2_147_483_647;
		b = (b * 48_271) % 2_147_483_647;

		if (a & 0xffff'i64 == b & 0xffff'i64) sum_ += 1;
	}

	close(f);

	println("part 1 = ", sum_);
	return str(sum_);
}

fn part2(): str
{
	let sum_ = 0'i64;

	let f = open(filename, "r");
	let a = i64(read_i32(readln(f)));
	let b = i64(read_i32(readln(f)));

	//println("a = ", a);
	//println("b = ", b);

	for i in [0: 5_000_000]
	{
		a = (a * 16_807) % 2_147_483_647;
		while (a % 4 != 0)
			a = (a * 16_807) % 2_147_483_647;

		b = (b * 48_271) % 2_147_483_647;
		while (b % 8 != 0)
			b = (b * 48_271) % 2_147_483_647;

		if (a & 0xffff'i64 == b & 0xffff'i64) sum_ += 1;
	}

	close(f);

	println("part 2 = ", sum_);
	return str(sum_);
}

//==============================================================================

fn main(): str
{
	// For unit-testing (within the syntran repo), this main fn returns a value
	// to be compared with expected results
	println();
	println("Starting AOC syntran main 2017/15");

	let p1 = ""; let p2 = "";

	p1 = part1();
	p2 = part2();

	println("Ending AOC syntran main");
	println();
	return p1 + ":" + p2;
}

//==============================================================================

return main();

//==============================================================================

